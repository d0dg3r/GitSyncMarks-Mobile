plugins {
    id "com.android.application"
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}
def requiredReleaseSigningKeys = ["keyAlias", "keyPassword", "storeFile", "storePassword"]

android {
    namespace = "com.d0dg3r.gitsyncmarks"
    compileSdk = flutter.compileSdkVersion

    dependenciesInfo {
        includeInApk = false
    }
    ndkVersion = "27.0.12077973"

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = "11"
    }

    defaultConfig {
        applicationId = "com.d0dg3r.gitsyncmarks"
        minSdk = flutter.minSdkVersion
        targetSdk = flutter.targetSdkVersion
        versionCode = flutter.versionCode
        versionName = flutter.versionName
    }

    signingConfigs {
        release {
            if (!keystorePropertiesFile.exists()) {
                throw new GradleException("Missing android/key.properties for release signing.")
            }
            def missingKeys = requiredReleaseSigningKeys.findAll { !keystoreProperties[it] }
            if (!missingKeys.isEmpty()) {
                throw new GradleException("Missing release signing keys in android/key.properties: ${missingKeys.join(', ')}")
            }
            def resolvedStoreFile = file(keystoreProperties['storeFile'])
            if (!resolvedStoreFile.exists()) {
                throw new GradleException("Release keystore file not found: ${resolvedStoreFile}")
            }
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile resolvedStoreFile
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        release {
            signingConfig = signingConfigs.release
        }
    }
}

flutter {
    source = "../.."
}
